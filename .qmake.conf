load(qt_build_config)
CONFIG += qt_example_installs

MODULE_VERSION = 5.2.0

# For the JS engine we need to be able to produce back traces,
# and as we're using the C stack, we need the system to be able
# to walk it properly. Unfortunately on Windows with i386 there
# are no unwind tables, that can compensate for an omitted frame
# pointer, so we have no choice but to disable the frame pointer
# omission optimizations.
# Only within the qtdeclarative module we support throwing V4
# exceptions, hence the choice of applying this change here.
win32:equals(QT_ARCH, "i386") {
    *msvc*: QMAKE_CXXFLAGS += -Oy-
    *g++*: QMAKE_CXXFLAGS += -fno-omit-frame-pointer
}

# Any code within this module has access to the private V4 API and
# can therefore be subject to exception traversal. Support for exceptions
# is therefore required, especially with older toolchains.
CONFIG += exceptions
